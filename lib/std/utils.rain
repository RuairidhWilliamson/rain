// utils.rain is designed to be removed in the future but provides useful APIs
let std = import("std.rain")
let {String, List, File, Bool, Any, Area} = std.types

pub let run = internal._run
pub let unit = internal._unit()
pub let bytes_to_string = internal._bytes_to_string
pub let parse_toml = internal._parse_toml
pub let local_area = internal._local_area
pub let split_string = internal._split_string
pub let index = internal._index
pub let host_info = internal._host_info
pub let string_contains = internal._string_contains
pub let export_to_local = internal._export_to_local
pub let check_export_to_local = internal._check_export_to_local
pub let glob = internal._glob
pub let stringify = internal._stringify

// Reserved keywords are prefixed with _
pub let _throw = internal._throw

pub let download = fn(url) {
	internal._download(url)
}

pub let download_with_sha256 = fn(url: String, sha256: String) -> File {
	download_result = internal._download(url)
	actual_sha256 = internal._sha256(download_result.file)
	if actual_sha256 != sha256 {
		internal._throw("bad sha256 " + actual_sha256)
	}
	download_result.file
}

pub let run_check = fn(area, bin, args: List(Any), env) {
	run_result = run(area, bin, args, env)
	if !run_result.success {
		print("Stdout:", run_result.stdout)
		print("Stderr:", run_result.stderr)
		_throw("run failed")
	}
	run_result
}

pub let newline = "\n"

pub let exe_suffix = fn(target_triple: String) -> String {
	if is_windows(target_triple) {
		".exe"
	} else {
		""
	}
}

pub let shared_library_prefix = fn(target_triple: String) -> String {
	if is_windows(target_triple) {
		""
	} else if is_linux(target_triple) {
		"lib"
	} else {
		_throw("unknown target: " + target_triple)
	}
}

pub let shared_library_suffix = fn(target_triple: String) -> String {
	if is_windows(target_triple) {
		".dll"
	} else if is_linux(target_triple) {
		".so"
	} else {
		_throw("unknown target: " + target_triple)
	}
}

pub let is_linux = fn(target_triple: String) -> Bool {
	string_contains(target_triple, "-linux-")
}

pub let is_windows = fn(target_triple: String) -> Bool {
	string_contains(target_triple, "-windows-")
}

pub let is_amd64 = fn(target_triple: String) -> Bool {
	string_contains(target_triple, "x86_64")
}

pub let path_var_separator = fn(target_triple) {
	if is_windows(target_triple) {
		";"
	} else {
		":"
	}
}
