let std = import("std.rain")

let download = std.utils.download
let _throw = std.utils._throw
let get_file = std.fs.file
let get_dir = std.fs.dir

let download_pre_sha = fn(url, filename) {
	file_url = url + "/" + filename
	sha256_download_result = download(file_url + ".sha256")
	if !sha256_download_result.ok {
		_throw("download sha256 failed")
	}
	download_result = download(file_url)
	if !download_result.ok {
		_throw("download failed")
	}
	expected = std.fs.read_file(sha256_download_result.file)
	actual = std.fs.sha256(download_result.file) + "  " + filename + std.utils.newline
	if expected != actual {
		_throw("checksum does not match '" + expected + "' != '" + actual + "'")
	}
	download_result.file
}


// Channel is a string one of stable|beta|nightly|<version>
let get_manifest = fn(channel) {
	base_url = "https://static.rust-lang.org"
	manifest_file = download_pre_sha(base_url + "/dist", "channel-rust-" + channel + ".toml")
	manifest_toml = std.utils.parse_toml(std.fs.read_file(manifest_file))
	if std.utils.index(manifest_toml, "manifest-version") != "2" {
		_throw("unsupported manifest version, expected 2 but got " + manifest_toml.manifest_version)
	}
	manifest_toml
}

let get_gz_artifact = fn(artifact) {
	if !artifact.available {
		_throw("requested artifact not available")
	}
	download_result = download(artifact.url)
	if !download_result.ok {
		_throw("download failed")
	}
	hash = std.fs.sha256(download_result.file)
	if artifact.hash != hash {
		_throw("checksum does not match")
	}
	std.compression.extract_tar_gz(download_result.file)
}

let get_xz_artifact = fn(artifact) {
	if !artifact.available {
		_throw("requested artifact not available")
	}
	download_result = download(artifact.xz_url)
	if !download_result.ok {
		_throw("download failed")
	}
	hash = std.fs.sha256(download_result.file)
	if artifact.xz_hash != hash {
		_throw("checksum does not match")
	}
	std.compression.extract_tar_xz(download_result.file)
}

let get_version_path = fn(manifest) {
	// Just use the rustc version for everything
	version_split = std.utils.split_string(manifest.pkg.rustc.version, " ")
	version = std.utils.index(version_split, 0)
	if std.utils.string_contains(version, "beta") {
		"beta"
	} else if std.utils.string_contains(version, "nightly") {
		"nightly"
	} else {
		version
	}
}

pub let get_rustc = fn(channel, target) {
	manifest = get_manifest(channel)
	version = get_version_path(manifest)
	rustc_pkg = get_xz_artifact(std.utils.index(manifest.pkg.rustc.target, target))
	rustc = get_dir(rustc_pkg, "rustc-" + version + "-" + target + "/rustc")
	rustc
}

pub let get_rust_std = fn(channel, target) {
	manifest = get_manifest(channel)
	version = get_version_path(manifest)
	pkg_targets = std.utils.index(manifest.pkg, "rust-std")
	rust_std_pkg = get_xz_artifact(std.utils.index(pkg_targets.target, target))
	rust_std = get_dir(rust_std_pkg, "rust-std-" + version + "-" + target + "/rust-std-" + target)
	rust_std
}

pub let get_cargo = fn(channel, target) {
	manifest = get_manifest(channel)
	version = get_version_path(manifest)
	cargo_pkg = get_xz_artifact(std.utils.index(manifest.pkg.cargo.target, target))
	cargo = get_dir(cargo_pkg, "cargo-" + version + "-" + target + "/cargo")
	cargo
}

pub let get_clippy = fn(channel, target) {
	manifest = get_manifest(channel)
	version = get_version_path(manifest)
	pkg_targets = std.utils.index(manifest.pkg, "clippy-preview")
	clippy_pkg = get_xz_artifact(std.utils.index(pkg_targets.target, target))
	clippy = get_dir(clippy_pkg, "clippy-" + version + "-" + target + "/clippy-preview")
	clippy
}

pub let get_rustfmt = fn(channel, target) {
	manifest = get_manifest(channel)
	version = get_version_path(manifest)
	pkg_targets = std.utils.index(manifest.pkg, "rustfmt-preview")
	rustfmt_pkg = get_xz_artifact(std.utils.index(pkg_targets.target, target))
	rustfmt = get_dir(rustfmt_pkg, "rustfmt-" + version + "-" + target + "/rustfmt-preview")
	rustfmt
}

pub let get_llvm_tools = fn(channel, target) {
	manifest = get_manifest(channel)
	version = get_version_path(manifest)
	pkg_targets = std.utils.index(manifest.pkg, "llvm-tools-preview")
	llvm_tools_pkg = get_xz_artifact(std.utils.index(pkg_targets.target, target))
	rustfmt = get_dir(llvm_tools_pkg, "llvm-tools-" + version + "-" + target + "/llvm-tools-preview")
	rustfmt
}
