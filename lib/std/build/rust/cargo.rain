let area = internal._get_area(internal._module_file())
let utils = internal._import(internal._get_file(area, "utils.rain"))
let rust_dist = internal._import(internal._get_file("dist.rain"))

let get_file = utils.get_file
let get_dir = utils.get_dir
let escape_bin = utils.escape_bin
let run = utils.run
let unit = utils.unit
let _throw = utils._throw
let print = utils.print

let Bool = std.types.Bool
let File = std.types.File

let host_triple = utils.host_info().triple
let path_var_sep = utils.path_var_separator(utils.host_info().triple)

let prepare_cargo = fn(channel) {
	// Implicitly requires cc for linking
	cc = escape_bin("cc")

	// Download components
	rustc = rust_dist.get_rustc(channel, host_triple)
	rust_std = rust_dist.get_rust_std(channel, host_triple)
	cargo = rust_dist.get_cargo(channel, host_triple)
	rustfmt = rust_dist.get_rustfmt(channel, host_triple)
	clippy = rust_dist.get_clippy(channel, host_triple)

	merged = utils.create_area([rustc, rust_std, cargo, rustfmt, clippy])
	bin_dir = get_dir(merged, "bin")
	env = {
		PATH = "/usr/bin" + path_var_sep + utils.stringify(bin_dir),
		LD_LIBRARY_PATH = get_dir(merged, "lib"),
		CARGO = get_file(bin_dir, "rustc"),
	}
	cargo_bin = get_file(bin_dir, "cargo")
	{env = env, cargo_bin = cargo_bin}
}

let build = fn(channel, area, package, default_features: Bool, features, profile, target, extra_args, env) -> File {
	prep = prepare_cargo(channel)
	args = [
		"build",
		"--package", package,
		"--features", features,
		"--target", target,
		"--profile", profile,
		"--target-dir", "target",
		"--quiet",
		"--color", "always"
	]
	if !default_features {
		args = args + ["--no-default-features"]
	}
	run_result = run(area, prep.cargo_bin, args + extra_args, internal._merge_records(prep.env, env))
	if !run_result.success {
		print(run_result.stderr)
		_throw("cargo compilation failed")
	}
	profile_dir = profile
	if profile == "dev" {
		profile_dir = "debug"
	}
	get_file(run_result.area, "target/" + target + "/" + profile_dir + "/" + package + utils.exe_suffix(target))
}

let test = fn(channel, area, package, default_features: Bool, features, profile) {
	prep = prepare_cargo(channel)
	args = [
		"test",
		"--package", package,
		"--features", features,
		"--profile", profile,
		"--target-dir", "target",
		"--color", "always",
		"--quiet"
	]
	if !default_features {
		args = args + ["--no-default-features"]
	}
	run_result = run(area, prep.cargo_bin, args, prep.env)
	if !run_result.success {
		print("Stdout:", run_result.stdout)
		print("Stderr:", run_result.stderr)
		_throw("cargo test failed")
	}
}

let clippy = fn(channel, area) {
	// Download components
	// Implicitly requires cc for linking
	cc = escape_bin("cc")

	// Download components
	rustc = rust_dist.get_rustc(channel, host_triple)
	rust_std = rust_dist.get_rust_std(channel, host_triple)
	cargo = rust_dist.get_cargo(channel, host_triple)
	clippy = rust_dist.get_clippy(channel, host_triple)

	merged = utils.create_area([rustc, rust_std, cargo, clippy])
	env = {PATH = "/usr/bin" + path_var_sep + utils.stringify(get_dir(merged, "/bin")), LD_LIBRARY_PATH = get_dir(merged, "/lib")}
	cargo_bin = get_file(merged, "/bin/cargo")
	run_result = run(area, cargo_bin, ["clippy", "--color", "always", "--quiet", "--", "-Dwarnings"], env)
	if !run_result.success {
		print(run_result.stderr)
		_throw("clippy failed")
	}
}

let check_fmt = fn(channel, area) {
	// Download components
	prep = prepare_cargo(channel)
	rustfmt = rust_dist.get_rustfmt(channel, host_triple)
	run_result = run(area, prep.cargo_bin, ["fmt", "--check", "--", "--color", "always"], prep.env)
	if !run_result.success {
		print(run_result.stdout)
		print(run_result.stderr)
		_throw("rustfmt check failed")
	}
}
