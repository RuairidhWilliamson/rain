let std = import("std.rain")

let {File, String, List} = std.types
let {run, unit, _throw} = std.utils

let escape_bin = std.escape.bin
let get_file = std.fs.file
let get_dir = std.fs.dir

let zig: File = std.build.zig.dist.download("0.14.1", "x86_64", "linux")
let zig_linker: File = std.fs.create_executable_file("#!/bin/sh\n" + std.utils.stringify(zig) + " cc \"$@\"", "zig_linker4.sh")

let compile = fn(channel, src: File, outname: String, target: String, edition: String, extra_args: List(String)) {
	// Download components
	rustc = std.build.rust.dist.get_rustc(channel, std.utils.host_info().triple)
	rust_std = std.build.rust.dist.get_rust_std(channel, target)

	merged = std.fs.create_area([rustc, rust_std])
	env = {
		CARGO_TERM_COLOR = "always",
		CARGO_TERM_QUIET = "true",
		LD_LIBRARY_PATH = get_dir(merged, "/lib"),
		ZIG_GLOBAL_CACHE_DIR = ".",
	}
	rustc_bin = get_file(merged, "/bin/rustc")
	args = [
		src,
		"-o", outname,
		"--target", target,
		"--edition", edition,
		"--emit", "dep-info,link",
		"-C", "linker=" + std.utils.stringify(zig_linker),
	] + extra_args
	run_result = run(unit, rustc_bin, args, env)
	if !run_result.success {
		std.utils.print(run_result.stderr)
		_throw("rustc compilation failed")
	}
	get_file(run_result.area, outname + std.utils.exe_suffix(target))
}
