let area = internal._get_area(internal._module_file())
let utils = internal._import(internal._get_file(area, "utils.rain"))
let rust_dist = internal._import(internal._get_file(area, "rust/dist.rain"))

let not = utils.not
let get_file = utils.get_file
let get_dir = utils.get_dir
let escape_bin = utils.escape_bin
let run = utils.run
let unit = utils.unit
let _throw = utils._throw

let host_triple = utils.host_info().triple

fn prepare_cargo(channel) {
	// Implicitly requires cc for linking
	cc = escape_bin("cc")

	// Download components
	rustc = rust_dist.get_rustc(channel, host_triple)
	rust_std = rust_dist.get_rust_std(channel, host_triple)
	cargo = rust_dist.get_cargo(channel, host_triple)

	merged = utils.create_area([rustc, rust_std, cargo])
	env = {PATH: "/usr/bin", LD_LIBRARY_PATH: get_dir(merged, "/lib")}
	cargo_bin = get_file(merged, "/bin/cargo")
	{env: env, cargo_bin: cargo_bin}
}

fn build(channel, area, package, features, profile, target) {
	prep = prepare_cargo(channel)
	run_result = run(area, prep.cargo_bin, ["build", "--quiet", "--package", package, "--features", features, "--target-dir", "target", "--profile", profile, "--color", "always", "--target", target], prep.env)
	if not(run_result.success) {
		utils.print(run_result.stderr)
		_throw("cargo compilation failed")
	}
	profile_dir = profile
	if profile == "dev" {
		profile_dir = "debug"
	}
	get_file(run_result.area, "target/" + target + "/" + profile_dir + "/" + package + utils.exe_suffix(target))
}

fn test(channel, area, package, features, profile) {
	prep = prepare_cargo(channel)
	run_result = run(area, prep.cargo_bin, ["test", "--quiet", "--package", package, "--features", features, "--profile", profile, "--color", "always"], prep.env)
	if not(run_result.success) {
		utils.print("Stdout:", run_result.stdout)
		utils.print("Stderr:", run_result.stderr)
		_throw("cargo test failed")
	}
}

fn clippy(channel, area) {
	// Download components
	// Implicitly requires cc for linking
	cc = escape_bin("cc")

	// Download components
	rustc = rust_dist.get_rustc(channel, host_triple)
	rust_std = rust_dist.get_rust_std(channel, host_triple)
	cargo = rust_dist.get_cargo(channel, host_triple)
	clippy = rust_dist.get_clippy(channel, host_triple)

	merged = utils.create_area([rustc, rust_std, cargo, clippy])
	path_sep = utils.path_separator(utils.host_info().triple)
	env = {PATH: "/usr/bin" + path_sep + utils.stringify(get_dir(merged, "/bin")), LD_LIBRARY_PATH: get_dir(merged, "/lib")}
	cargo_bin = get_file(merged, "/bin/cargo")
	run_result = run(area, cargo_bin, ["clippy", "--color", "always", "-q", "--", "-Dwarnings"], env)
	if not(run_result.success) {
		utils.print(run_result.stderr)
		_throw("clippy failed")
	}
}

fn check_fmt(channel, area) {
	// Download components
	rustfmt = rust_dist.get_rustfmt(channel, host_triple)

	env = {}
	cargo_fmt_bin = get_file(rustfmt, "bin/cargo-fmt")
	run_result = run(area, cargo_fmt_bin, ["--check", "--", "--color", "always"], env)
	if not(run_result.success) {
		utils.print(run_result.stdout)
		_throw("rustfmt check failed")
	}
}
