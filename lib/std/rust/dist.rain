let area = internal._get_area(internal._module_file())
let utils = internal._import(internal._get_file(area, "utils.rain"))

let not = utils.not
let download = utils.download
let _throw = utils._throw
let get_file = utils.get_file
let get_dir = utils.get_dir

fn download_pre_sha(url, filename) {
	file_url = url + "/" + filename
	sha256_download_result = download(file_url + ".sha256")
	if not(sha256_download_result.ok) {
		_throw("download sha256 failed")
	}
	download_result = download(file_url)
	if not(download_result.ok) {
		_throw("download failed")
	}
	expected = utils.read_file(sha256_download_result.file)
	actual = utils.sha256(download_result.file) + "  " + filename + utils.newline
	if expected != actual {
		_throw("checksum does not match '" + expected + "' != '" + actual + "'")
	}
	download_result.file
}

let base_url = "https://static.rust-lang.org"

// Channel is a string one of stable|beta|nightly|<version>
fn get_manifest(channel) {
	manifest_file = download_pre_sha(base_url + "/dist", "channel-rust-" + channel + ".toml")
	manifest_toml = utils.parse_toml(utils.read_file(manifest_file))
	if manifest_toml.manifest_version != "2" {
		_throw("unsupported manifest version, expected 2 but got " + manifest_toml.manifest_version)
	}
	manifest_toml
}

fn get_gz_artifact(artifact) {
	if not(artifact.available) {
		_throw("requested artifact not available")
	}
	download_result = download(artifact.url)
	if not(download_result.ok) {
		_throw("download failed")
	}
	hash = utils.sha256(download_result.file)
	if artifact.hash != hash {
		_throw("checksum does not match")
	}
	utils.extract_tar_gz(download_result.file)
}

fn get_xz_artifact(artifact) {
	if not(artifact.available) {
		_throw("requested artifact not available")
	}
	download_result = download(artifact.xz_url)
	if not(download_result.ok) {
		_throw("download failed")
	}
	hash = utils.sha256(download_result.file)
	if artifact.xz_hash != hash {
		_throw("checksum does not match")
	}
	utils.extract_tar_xz(download_result.file)
}

// TODO: Parse the version from the manifest
let version = "1.85.1"
// TODO: Specify the target
let target = "x86_64-unknown-linux-gnu"

fn get_rustc(channel) {
	manifest = get_manifest(channel)
	rustc_pkg = get_xz_artifact(manifest.pkg.rustc.target.x86_64_unknown_linux_gnu)
	rustc = get_dir(rustc_pkg, "rustc-" + version + "-" + target + "/rustc")
	rustc
}

fn get_rust_std(channel) {
	manifest = get_manifest(channel)
	rust_std_pkg = get_xz_artifact(manifest.pkg.rust_std.target.x86_64_unknown_linux_gnu)
	rust_std = get_dir(rust_std_pkg, "rust-std-" + version + "-" + target + "/rust-std-x86_64-unknown-linux-gnu/")
	rust_std
}

fn get_cargo(channel) {
	manifest = get_manifest(channel)
	cargo_pkg = get_xz_artifact(manifest.pkg.cargo.target.x86_64_unknown_linux_gnu)
	cargo = get_dir(cargo_pkg, "cargo-" + version + "-" + target + "/cargo")
	cargo
}

fn get_clippy(channel) {
	manifest = get_manifest(channel)
	clippy_pkg = get_xz_artifact(manifest.pkg.clippy_preview.target.x86_64_unknown_linux_gnu)
	clippy = get_dir(clippy_pkg, "clippy-" + version + "-" + target + "/clippy-preview")
	clippy
}

fn get_rustfmt(channel) {
	manifest = get_manifest(channel)
	rustfmt_pkg = get_xz_artifact(manifest.pkg.rustfmt_preview.target.x86_64_unknown_linux_gnu)
	rustfmt = get_dir(rustfmt_pkg, "rustfmt-" + version + "-" + target + "/rustfmt-preview")
	rustfmt
}
