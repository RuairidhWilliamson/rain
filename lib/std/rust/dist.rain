let utils = internal._import(internal._get_file("../utils.rain"))

let not = utils.not
let download = utils.download
let _throw = utils._throw

fn download_pre_sha(url, filename) {
	file_url = url + "/" + filename
	sha256_download_result = download(file_url + ".sha256")
	if not(sha256_download_result.ok) {
		_throw("download sha256 failed")
	}
	download_result = download(file_url)
	if not(download_result.ok) {
		_throw("download failed")
	}
	expected = utils.read_file(sha256_download_result.file)
	actual = utils.sha256(download_result.file) + "  " + filename + utils.newline
	if expected != actual {
		_throw("checksum does not match '" + expected + "' != '" + actual + "'")
	}
	download_result.file
}

let base_url = "https://static.rust-lang.org"

fn get_manifest(channel) {
	manifest_file = download_pre_sha(base_url + "/dist", "channel-rust-" + channel + ".toml")
	manifest_toml = utils.parse_toml(utils.read_file(manifest_file))
	if manifest_toml.manifest_version != "2" {
		_throw("unsupported manifest version, expected 2 but got " + manifest_toml.manifest_version)
	}
	manifest_toml
}

fn get_artifact(artifact) {
	if not(artifact.available) {
		_throw("requested artifact not available")
	}
	download_result = download(artifact.url)
	if not(download_result.ok) {
		_throw("download failed")
	}
	hash = utils.sha256(download_result.file)
	if artifact.hash != hash {
		_throw("checksum does not match")
	}
	utils.extract_tar_gz(download_result.file)
}

fn get_xz_artifact(artifact) {
	if not(artifact.available) {
		_throw("requested artifact not available")
	}
	download_result = download(artifact.xz_url)
	if not(download_result.ok) {
		_throw("download failed")
	}
	hash = utils.sha256(download_result.file)
	if artifact.xz_hash != hash {
		_throw("checksum does not match")
	}
	utils.extract_tar_xz(download_result.file)
}

fn get(channel) {
	manifest = get_manifest(channel)
	rustc = get_xz_artifact(manifest.pkg.rustc.target.x86_64_unknown_linux_gnu)
	cargo = get_xz_artifact(manifest.pkg.cargo.target.x86_64_unknown_linux_gnu)
	{rustc: rustc, cargo: cargo}
}

let stable = get("stable")
let beta = get("beta")
let nightly = get("nightly")
