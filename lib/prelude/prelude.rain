let String = internal._get_type("")

pub let download_stdlib = fn(version: String) {
	bucket = "rain-e870e063"
	url = "https://storage.googleapis.com/" + bucket + "/releases/" + version + "/stdlib.tar.gz"
	filename = "stdlib.tar.gz"
	sha256_download_result = internal._download(url + ".sha256")
	if !sha256_download_result.ok {
		internal._throw("download sha256 failed")
	}
	download_result = internal._download(url)
	if !download_result.ok {
		internal._throw("failed to download stdlib")
	}
	expected = internal._read_file(sha256_download_result.file)
	actual = internal._sha256(download_result.file)
	if expected != actual {
		internal._throw("checksum does not match '" + expected + "' != '" + actual + "'")
	}
	lib_area = internal._extract_tar(internal._extract_gzip(download_result.file, "temp.tar"))
	stdlib_area = internal._create_area([internal._get_dir(lib_area, "lib/std")])
	load_stdlib(stdlib_area)
}

pub let load_stdlib = fn(area) {
	internal._import(internal._get_file(area, "std.rain"))
}

pub let import_sugar_implementation = fn(module_file) {
	fn(path) {
		internal._import(internal._get_file(internal._get_area(module_file), path))
	}
}

pub let stdlib_sugar_implementation = download_stdlib
