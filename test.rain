let main = import("main.rain")
let std = main.std

let {print} = std

// Run tests to make sure intra-process caching is working

pub let test_caching = fn() {
	test_embed_cache()
	test_rustc_cache()
	test_create_file_cache()
	test_import_cache()
	test_type_check()
}

let test_embed_cache = fn() {
	std1 = internal._embed().download_stdlib("0.1.4")
	std2 = internal._embed().download_stdlib("0.1.4")
	std.assert.is_true(internal._rust_eq(std1, std2))
}

let rustc_helloworld = fn() {
	src = std.fs.create_file("fn main() {\n\tprintln!(\"Hello world!\")\n}", "helloworld.rs")
	std.build.rust.rustc.compile("stable", src, "helloworld", std.utils.host_info().triple, "2024", [])
}

let test_rustc_cache = fn() {
	bin1 = rustc_helloworld()
	bin2 = rustc_helloworld()
	std.assert.is_true(internal._rust_eq(bin1, bin2))
}

let test_create_file_cache = fn() {
	contents = "hello world"
	name = "helloworld.txt"
	file1 = std.fs.create_file(contents, name)
	file2 = std.fs.create_file(contents, name)
	std.assert.is_true(internal._rust_eq(file1, file2))
}

let test_import_cache = fn() {
	rain_src = std.fs.create_file("let foo = 5", "foo.rain")
	main1 = internal._import(rain_src)
	main2 = internal._import(rain_src)
	std.assert.is_true(internal._rust_eq(main1, main2))
}

let test_type_check = fn() {
	a = "abcd"
	std.assert.not_equal(internal._get_type(a), std.types.Integer)
	std.assert.equal(internal._get_type(a), std.types.String)
}

let bar = fn() {
	foo()
}

let foo = fn() {
	internal._throw("hi")
}
