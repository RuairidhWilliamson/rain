let area = internal._get_area(internal._module_file())
let main = internal._import(internal._get_file(area, "main.rain"))
let std = main.std

let print = std.utils.print
let run = std.utils.run
let unit = std.utils.unit
let escape_bin = std.utils.escape_bin
let get_dir = std.utils.get_dir
let get_file = std.utils.get_file
let _throw = std.utils._throw

let bucket = "rain-e870e063"

fn compress_lib() {
	ouch = escape_bin("ouch")
	lib_dir = get_dir(area, "lib")
	filename = "lib.tar.gz"
	run_result = run(unit, ouch, ["compress", lib_dir, filename], {})
	if !run_result.success {
		print("Stdout:", run_result.stdout)
		print("Stderr:", run_result.stderr)
		_throw("ouch compress failed")
	}
	get_file(run_result.area, filename)
}

fn upload_gcloud(src, dst) {
	gcloud = escape_bin("gcloud")
	run_result = run(unit, gcloud, ["storage", "cp", src, dst, "--no-clobber"], {})
	print("Stdout:", run_result.stdout)
	print("Stderr:", run_result.stderr)
	if !run_result.success {
		_throw("gcloud upload failed")
	}
}

fn publish(version) {
	prefix = "gs://" + bucket + "/releases/" + version
	upload_gcloud(compress_lib(), prefix + "/stdlib.tar.gz")
	win_amd64 = main.build_release("x86_64-pc-windows-gnu")
	upload_gcloud(win_amd64, prefix + "/rain-windows-amd64.exe")
	linux_amd64 = main.build_release("x86_64-unknown-linux-gnu")
	upload_gcloud(linux_amd64, prefix + "/rain-linux-amd64")
	print("Published ", version)
}
