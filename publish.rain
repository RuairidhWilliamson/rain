let main = import("main.rain")
let std = main.std

let {run, unit, _throw, parse_toml} = std.utils
let {print} = std
let {File, Dir, String} = std.types

let area = std.fs.area(this_file)
let bucket = "rain-e870e063"
let webhook_url = internal._get_secret("RAIN_PUBLISH_WEBHOOK")
let gcloud = std.escape.bin("gcloud")

let check_version = fn(version: String) {
	cargo_toml = std.fs.file(area, "Cargo.toml")
	cargo_workspace_version = parse_toml(std.fs.read_file(cargo_toml)).workspace.package.version
	if cargo_workspace_version != version {
		_throw("cargo workspace version does not match " + cargo_workspace_version +  " != " + version)
	}
}

let compress_lib = fn() {
	lib_dir = std.fs.dir(area, "lib/std")
	filename = "stdlib.tar.gz"
	std.compression.compress_tar_gz(lib_dir, filename)
}

let upload_gcloud = fn(src: File, dst: String) {
	run_result = run(unit, gcloud, ["storage", "cp", src, dst, "--no-clobber"], {})
	print("Stdout:", run_result.stdout)
	print("Stderr:", run_result.stderr)
	if !run_result.success {
		_throw("gcloud upload failed")
	}
}

let upload_gcloud_recursive = fn(src: Dir, dst: String) {
	run_result = run(unit, gcloud, ["storage", "cp", src, dst, "--recursive"], {})
	print("Stdout:", run_result.stdout)
	print("Stderr:", run_result.stderr)
	if !run_result.success {
		_throw("gcloud upload failed")
	}
}

let upload_gcloud_overwrite = fn(src: File, dst: String) {
	run_result = run(unit, gcloud, ["storage", "cp", src, dst], {})
	print("Stdout:", run_result.stdout)
	print("Stderr:", run_result.stderr)
	if !run_result.success {
		_throw("gcloud upload failed")
	}
}

let upload_gcloud_with_sha256 = fn(src: File, dst: String) {
	sha256sum = std.fs.create_file(std.fs.sha256(src), "sha256")
	run_result = run(unit, gcloud, ["storage", "cp", src, dst, "--no-clobber"], {})
	print("Stdout:", run_result.stdout)
	print("Stderr:", run_result.stderr)
	if !run_result.success {
		_throw("gcloud upload failed")
	}
	run_result = run(unit, gcloud, ["storage", "cp", sha256sum, dst + ".sha256", "--no-clobber"], {})
	print("Stdout:", run_result.stdout)
	print("Stderr:", run_result.stderr)
	if !run_result.success {
		_throw("gcloud sha256 upload failed")
	}
}

let webhook = fn(url, contents) {
	std.escape.run(area, std.escape.bin("xhs"), ["POST", url, "content=" + contents, "--ignore-stdin"], {})
	unit
}

pub let publish_tree_sitter_rain_crate = fn(version: String) {
	main.tree_sitter_rain.build()
	cargo = std.escape.bin("cargo")
	check_version(version)
	tree_sitter_rain_dir = std.fs.dir(area, "tree-sitter-rain")
	std.escape.run(tree_sitter_rain_dir, cargo, ["package", "--index", "sparse+https://storage.googleapis.com/" + bucket + "/crates/index/"], main.cargo_env)
	crate = std.fs.file(area, "target/package/tmp-registry/tree-sitter-rain-" + version + ".crate")
	new_index = std.fs.file(area, "target/package/tmp-registry/index/tr/ee/tree-sitter-rain")
	download = std.utils.download("https://storage.googleapis.com/" + bucket + "/crates/index/tr/ee/tree-sitter-rain")
	if download.ok {
		index = std.fs.create_file(std.fs.read_file(download.file) + "\n" + std.fs.read_file(new_index), "tree-sitter-rain")
	} else {
		index = new_index
	}
	upload_gcloud(crate, "gs://" + bucket + "/crates/download/tree-sitter-rain/" + version)
	upload_gcloud_overwrite(index, "gs://" + bucket + "/crates/index/tr/ee/tree-sitter-rain")
	print("Published crate tree-sitter-rain ", version)
}

pub let build = fn(version: String) {
	check_version(version)
	{
		win_amd64 = main.build_release("x86_64-pc-windows-gnu"),
		linux_amd64 = main.build_release("x86_64-unknown-linux-gnu"),
		ci_web = main.ci_mod.build_ci_web(),
		ci_migrator = main.ci_mod.build_ci_migrator(),
		ci_coordinator = main.ci_mod.build_ci_coordinator(),
	}
}

pub let publish = fn(version: String) {
	check_version(version)
	built = build(version)
	win_amd64 = built.win_amd64
	linux_amd64 = built.linux_amd64
	ci_web = built.ci_web
	ci_migrator = built.ci_migrator
	ci_coordinator = built.ci_coordinator

	prefix = "gs://" + bucket + "/releases/" + version
	upload_gcloud_with_sha256(compress_lib(), prefix + "/stdlib.tar.gz")
	upload_gcloud_with_sha256(win_amd64, prefix + "/rain-windows-amd64.exe")
	upload_gcloud_with_sha256(linux_amd64, prefix + "/rain-linux-amd64")
	upload_gcloud_with_sha256(ci_web, prefix + "/rain-ci-web")
	upload_gcloud_with_sha256(ci_migrator, prefix + "/rain-ci-migrator")
	upload_gcloud_with_sha256(ci_coordinator, prefix + "/rain-ci-coordinator")
	windows_url = "https://storage.googleapis.com/" + bucket + "/releases/" + version + "/rain-windows-amd64.exe"
	linux_url = "https://storage.googleapis.com/" + bucket + "/releases/" + version + "/rain-linux-amd64"
	webhook(webhook_url, "Rain " + version + "\n" + windows_url + "\n" + linux_url)
	print("Published ", version)
}
