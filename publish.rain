let main = import("main.rain")
let std = main.std

let {run, unit, _throw, parse_toml} = std.utils
let {print} = std

let area = std.fs.area(this_file)
let bucket = "rain-e870e063"
let webhook_url = internal._get_secret("RAIN_PUBLISH_WEBHOOK")

let check_version = fn(version) {
	cargo_toml = std.fs.file(area, "Cargo.toml")
	cargo_workspace_version = parse_toml(std.fs.read_file(cargo_toml)).workspace.package.version
	if cargo_workspace_version != version {
		_throw("cargo workspace version does not match " + cargo_workspace_version +  " != " + version)
	}
}

let compress_lib = fn() {
	ouch = std.escape.bin("ouch")
	lib_dir = std.fs.dir(area, "lib")
	filename = "lib.tar.gz"
	run_result = run(unit, ouch, ["compress", lib_dir, filename], {})
	if !run_result.success {
		print("Stdout:", run_result.stdout)
		print("Stderr:", run_result.stderr)
		_throw("ouch compress failed")
	}
	std.fs.file(run_result.area, filename)
}

let upload_gcloud = fn(src, dst) {
	gcloud = std.escape.bin("gcloud")
	run_result = run(unit, gcloud, ["storage", "cp", src, dst, "--no-clobber"], {})
	print("Stdout:", run_result.stdout)
	print("Stderr:", run_result.stderr)
	if !run_result.success {
		_throw("gcloud upload failed")
	}
}

let upload_gcloud_with_sha256 = fn(src, dst) {
	sha256sum = std.fs.create_file(std.fs.sha256(src), "sha256")
	gcloud = std.escape.bin("gcloud")
	run_result = run(unit, gcloud, ["storage", "cp", src, dst, "--no-clobber"], {})
	print("Stdout:", run_result.stdout)
	print("Stderr:", run_result.stderr)
	if !run_result.success {
		_throw("gcloud upload failed")
	}
	run_result = run(unit, gcloud, ["storage", "cp", sha256sum, dst + ".sha256", "--no-clobber"], {})
	print("Stdout:", run_result.stdout)
	print("Stderr:", run_result.stderr)
	if !run_result.success {
		_throw("gcloud sha256 upload failed")
	}
}

let webhook = fn(url, contents) {
	std.escape.run(area, std.escape.bin("xhs"), ["POST", url, "content=" + contents, "--ignore-stdin"], {})
	unit
}

pub let publish = fn(version) {
	check_version(version)
	win_amd64 = main.build_release("x86_64-pc-windows-gnu")
	linux_amd64 = main.build_release("x86_64-unknown-linux-gnu")
	prefix = "gs://" + bucket + "/releases/" + version
	upload_gcloud_with_sha256(compress_lib(), prefix + "/stdlib.tar.gz")
	upload_gcloud_with_sha256(win_amd64, prefix + "/rain-windows-amd64.exe")
	upload_gcloud_with_sha256(linux_amd64, prefix + "/rain-linux-amd64")
	windows_url = "https://storage.googleapis.com/" + bucket + "/releases/" + version + "/rain-windows-amd64.exe"
	linux_url = "https://storage.googleapis.com/" + bucket + "/releases/" + version + "/rain-linux-amd64"
	webhook(webhook_url, "Rain " + version + "\n" + windows_url + "\n" + linux_url)
	print("Published ", version)
}
