let area = internal._get_area(internal._module_file())
let std = load_stdlib()
let publish_module = internal._import(internal._get_file("./publish.rain"))
let test_mod = internal._import(internal._get_file(area, "test.rain"))
pub let examples = internal._import(internal._get_file(area, "examples/_.rain"))

fn load_stdlib() {
	stdlib_area = internal._create_area([internal._get_dir(area, "lib/std")])
	std = internal._import(internal._get_file(stdlib_area, "std.rain"))
	std
}

let print = std.utils.print
let _import = std.utils._import
let get_file = std.utils.get_file
let get_area = std.utils.get_area
let module_file = std.utils.module_file

let rust_version = "stable"

pub fn lint() {
	check_empty_files()
}

pub fn check() {
	lint()
	test()
}

fn build() {
	std.build.rust.cargo.build(std.build.rust.stable, area, "rain", true, "", "dev", std.utils.host_info().triple, [], {})
}

fn build_release(target_triple) {
	std.build.rust.cargo.build(std.build.rust.stable, area, "rain", false, "", "release", target_triple, [], {})
}

fn build_c() {
	std.build.cc.gcc.compile(get_file("core/examples/run/foo.c"), "foo")
}

fn run() {
	binary = build_c()
	std.utils.run(std.utils.unit, binary, [], {})
}

fn helloworld() {
	src = std.utils.create_file("fn main() {\n\tprintln!(\"Hello world!\")\n}", "helloworld.rs")
	bin = std.build.rust.rustc.compile("stable", src, "helloworld", std.utils.host_info().triple, "2024")
	print(std.utils.run(std.utils.unit, bin, [], {}).stdout)
}

fn check_empty_files() {
	std.utils.foreach(std.utils.glob(area), std.build.lints.no_emptish)
}

fn everything() {
	check()
	build()
	run()
	download_test()
	print_some_stuff()
}

fn test_zig() {
	src = std.utils.create_file("const std = @import(\"std\"); pub fn main() !void {std.debug.print(\"hello world\", .{});}", "helloworld.zig")
	zig_bin = std.build.zig.dist.download("0.14.1", "x86_64", "linux")
	run_result = std.utils.run(std.utils.unit, zig_bin, ["build-exe", src], {ZIG_GLOBAL_CACHE_DIR = "."})
	if !run_result.success {
		std.utils.print("Stdout:", run_result.stdout)
		std.utils.print("Stderr:", run_result.stderr)
		std.utils._throw("build zig")
	}
	bin = get_file(run_result.area, "helloworld")
	run_result = std.utils.run(std.utils.unit, bin, [], {})
	if !run_result.success {
		std.utils._throw("zig helloworld")
	}
	print(run_result.stderr)
}

fn publish(version) {
	publish_module.publish(version)
}

fn install_cli() {
	cargo = std.escape.bin("cargo")
	std.escape.run(area, cargo, ["install", "--path", "cli", "--locked", "-q"], {})
}

fn test() {
	test_mod.test_caching()
}
