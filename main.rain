let cargo = std.escape.bin("cargo")
let tokei = std.escape.bin("tokei")

fn escape_run(program, args) {
	let out = std.escape.run(program = program, args = args)
	if !out.success {
		core.print("=== STDOUT ===")
		core.print(out.stdout)
		core.print("==============")
		core.print("=== STDERR ===")
		core.print(out.stderr)
		core.print("==============")
		core.error("Command failed")
	}
}

# Runs all the checks
pub fn check() {
	clippy()
	fmt()
	tests()
	outdated()
}

# Run clippy
pub fn clippy() {
	escape_run(program = cargo, args = ["clippy", "--workspace", "--color", "always", "--", "--deny", "warnings"])
}

# Run rustfmt check
pub fn fmt() {
	escape_run(program = cargo, args = ["fmt", "--check"])
}

# Run all tests
pub fn tests() {
	escape_run(program = cargo, args = ["test", "--workspace", "--color", "always"])
}

# Check for outdated dependencies
pub fn outdated() {
	escape_run(program = cargo, args = ["outdated", "--exit-code", "1", "--color", "always"])
}

# Installs rain locally in cargo bin
pub fn install_locally() {
	check()
	core.print("Installing...")
	escape_run(program = cargo, args = ["install", "--path", "."])
}

# Prints info about language line numbers in the repo
pub fn code_info() {
	let out = std.escape.run(program = tokei, args = [])
	core.print(out.stdout)
}
